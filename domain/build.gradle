apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
  compileSdkVersion 28

  lintOptions {
    warningsAsErrors true
    abortOnError true
    xmlReport false
  }
  testOptions {
    unitTests {
      includeAndroidResources = true
    }
    execution "ANDROIDX_TEST_ORCHESTRATOR"
    animationsDisabled = true
  }
  defaultConfig {
    minSdkVersion 24
    targetSdkVersion 28
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    testInstrumentationRunnerArguments clearPackageData: 'true'
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
    }
  }

  sourceSets {
    String sharedTestDir = "src/sharedTest/java"
    test {
      java.srcDir sharedTestDir
    }
    androidTest {
      java.srcDir sharedTestDir
    }
  }

}

dependencies {

  implementation "androidx.appcompat:appcompat:1.1.0-alpha05"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.31"

  testImplementation "androidx.test:rules:1.2.0-beta01"
  testImplementation "androidx.test:runner:1.2.0-beta01"
  testImplementation "androidx.test.espresso:espresso-core:3.2.0-beta01"
  testImplementation "androidx.test.ext:junit-ktx:1.1.1-beta01"
  testImplementation "androidx.test.ext:truth:1.1.0"
  testImplementation "junit:junit:4.12"
  testImplementation "org.robolectric:robolectric:4.2.1"
  testImplementation "org.koin:koin-test:1.0.1"

  androidTestUtil "androidx.test:orchestrator:1.2.0-beta01"

  androidTestImplementation "androidx.test:runner:1.2.0-beta01"
  androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0-beta01"
  androidTestImplementation "androidx.test.ext:junit-ktx:1.1.1-beta01"

}

assemble.dependsOn assembleAndroidTest
