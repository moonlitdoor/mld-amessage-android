apply plugin: "com.moonlitdoor.git-version"
apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "com.github.triplet.play"
apply plugin: "io.fabric"
apply plugin: "androidx.navigation.safeargs"
//apply plugin: "com.google.firebase.firebase-perf"

project.ext.archivesBaseName = project.name + "-" + gitVersion

android {
  compileSdkVersion 28

  lintOptions {
    warningsAsErrors true
    abortOnError true
    xmlReport false
    explainIssues true
    showAll true
  }
  testOptions {
    unitTests {
      includeAndroidResources = true
    }
    if (orchestrator) {
      execution "ANDROID_TEST_ORCHESTRATOR"
    }
    animationsDisabled = true
  }
  playAccountConfigs {
    defaultAccountConfig {
      jsonFile = file("keys.json")
    }
  }
  defaultConfig {
    applicationId "com.moonlitdoor.amessage"
    minSdkVersion 24
    targetSdkVersion 28
    versionCode = gitCommitAndTagCount
    versionName = gitVersion
    playAccountConfig = playAccountConfigs.defaultAccountConfig
    buildConfigField "String", "BUILD_DATE", "\"0\""
    if (largeTests) {
      testInstrumentationRunner "com.moonlitdoor.amessage.AndroidJUnitRunnerLarge"
    } else if (mediumTests) {
      testInstrumentationRunner "com.moonlitdoor.amessage.AndroidJUnitRunnerMedium"
    } else if (smallTests) {
      testInstrumentationRunner "com.moonlitdoor.amessage.AndroidJUnitRunnerSmall"
    } else if (smokeTests) {
      testInstrumentationRunner "com.moonlitdoor.amessage.AndroidJUnitRunnerSmoke"
    } else if (flakyTests) {
      testInstrumentationRunner "com.moonlitdoor.amessage.AndroidJUnitRunnerFlaky"
    } else {
      testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    testInstrumentationRunnerArguments clearPackageData: "true"
    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }
  signingConfigs {
    release {
      storeFile file("keystore")
      storePassword "somePasswordFromTheCommandLine"
      keyAlias "somePasswordFromTheCommandLine"
      keyPassword "somePasswordFromTheCommandLine"
    }
  }
  buildTypes {
    release {
      buildConfigField "String", "BUILD_DATE", "\"${System.currentTimeMillis()}\""
      signingConfig signingConfigs.release
      //      minifyEnabled true
    }
    debug {
      minifyEnabled false
    }
  }
  packagingOptions {
    exclude "META-INF/proguard/androidx-annotations.pro"
  }
  dataBinding {
    enabled true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

play {
  track(getPlayTrack())
  untrackOld = true
}

dependencies {

//  kapt "androidx.lifecycle:lifecycle-compiler:2.2.0-alpha01"
  kapt "androidx.room:room-compiler:2.1.0-beta01"

  implementation project(":domain")

  implementation "androidx.lifecycle:lifecycle-runtime:2.2.0-alpha01"
  implementation "androidx.lifecycle:lifecycle-extensions:2.2.0-alpha01"
  implementation "androidx.room:room-runtime:2.1.0-beta01"
  implementation "androidx.room:room-coroutines:2.1.0-alpha04"
  implementation "androidx.navigation:navigation-fragment-ktx:2.1.0-alpha04"
  implementation "androidx.navigation:navigation-ui-ktx:2.1.0-alpha04"
  implementation "androidx.work:work-runtime-ktx:2.1.0-alpha02"
  implementation "androidx.core:core-ktx:1.2.0-alpha01"
  implementation "androidx.constraintlayout:constraintlayout:1.1.3"
  implementation "androidx.appcompat:appcompat:1.1.0-alpha05"
  implementation "com.google.android.material:material:1.0.0"
  implementation "androidx.preference:preference:1.0.0"
  implementation "androidx.legacy:legacy-preference-v14:1.0.0"
  implementation "androidx.recyclerview:recyclerview:1.0.0"
  implementation "androidx.legacy:legacy-support-v4:1.0.0"
  implementation "com.crashlytics.sdk.android:crashlytics:2.10.1"
  implementation "com.google.android.gms:play-services-vision:17.0.2"
  implementation "com.google.code.gson:gson:2.8.5"
  implementation "com.google.firebase:firebase-config:17.0.0"
  implementation "com.google.firebase:firebase-core:16.0.9"
  implementation "com.google.firebase:firebase-messaging:18.0.0"
//  implementation "com.google.firebase:firebase-perf:17.0.0"
  implementation "com.google.android.material:material:1.1.0-alpha06"
  implementation "com.google.zxing:core:3.3.0"
  implementation "com.jakewharton.timber:timber:4.7.1"
  implementation "com.moonlitdoor.shared-preference-live-data:shared-preference-live-data:0.0.6"
  implementation "com.stepstone.stepper:material-stepper:4.3.1"
  implementation "com.squareup.okhttp3:logging-interceptor:3.12.1"
  implementation "com.squareup.retrofit2:converter-gson:2.5.0"
  implementation "com.squareup.retrofit2:retrofit:2.5.0"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.31"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"
  implementation "org.koin:koin-android:1.0.1"
  implementation "org.koin:koin-android-scope:1.0.1"
  implementation "org.koin:koin-android-viewmodel:1.0.1"

  implementation "androidx.test.espresso:espresso-idling-resource:3.2.0-beta01"

  androidTestImplementation "androidx.test:runner:1.2.0-beta01"
  androidTestImplementation "androidx.test:rules:1.2.0-beta01"
  androidTestImplementation "androidx.room:room-testing:2.0.0"
  androidTestImplementation "android.arch.work:work-testing:1.0.1"
  androidTestImplementation "androidx.legacy:legacy-support-core-utils:1.0.0"
  androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0-beta01"
  androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:3.2.0-beta01"
  androidTestImplementation "android.arch.navigation:navigation-testing:1.0.0-alpha08"

  androidTestUtil "androidx.test:orchestrator:1.2.0-beta01"

  testImplementation "androidx.test:runner:1.2.0-beta01"
  testImplementation "androidx.test:rules:1.2.0-beta01"
  testImplementation "org.robolectric:robolectric:4.2.1"
  testImplementation "androidx.room:room-testing:2.0.0"
  testImplementation "androidx.legacy:legacy-support-core-utils:1.0.0"
  testImplementation "androidx.test.espresso:espresso-core:3.2.0-beta01"
  testImplementation "androidx.legacy:legacy-support-core-utils:1.0.0"
  testImplementation "org.koin:koin-test:1.0.1"
  testImplementation "com.squareup.okhttp3:mockwebserver:3.11.0"
  testImplementation "junit:junit:4.12"

}

apply plugin: "com.google.gms.google-services"

task connectedLargeTests {
  dependsOn "connectedCheck"
  doFirst {

  }
}


task parseMetadata() {
  ext.parse = { param1, param2 ->
    rootProject.file(String.format("amessage/src/main/play/%s", param1)).mkdir()
    def useDefault = true
    def file = rootProject.file(String.format("amessage/src/main/play/%s/whatsnew", param1))
    file.delete()
    def parsedStrings = new XmlParser().parse(rootProject.file(String.format("amessage/src/main/res/%s/strings.xml", param2)))
    parsedStrings["string-array"].each { stringArray ->
      if (stringArray.@name == "metadata_whats_new") {
        stringArray.item.each { item ->
          useDefault = false
          file.append(item.text().replace("\\", "") + "\n")
        }
      }
    }
    rootProject.file(String.format("amessage/src/main/play/%s/listing", param1)).mkdir()
    parsedStrings.string.each { string ->
      if (string.@name == "metadata_title") {
        file = rootProject.file(String.format("amessage/src/main/play/%s/listing/title", param1))
        file.delete()
        file.append(string.text().replace("\\", ""))
      } else if (string.@name == "metadata_short_description") {
        file = rootProject.file(String.format("amessage/src/main/play/%s/listing/shortdescription", param1))
        file.delete()
        file.append(string.text().replace("\\", ""))
      } else if (string.@name == "metadata_full_description") {
        file = rootProject.file(String.format("amessage/src/main/play/%s/listing/fulldescription", param1))
        file.delete()
        file.append(string.text().replace("\\", ""))
      } else if (string.@name == "metadata_whats_new_bug_fixes" && useDefault) {
        file = rootProject.file(String.format("amessage/src/main/play/%s/whatsnew", param1))
        file.delete()
        file.append(string.text().replace("\\", ""))
      }
    }
  }
  doLast {
    parse("en-US", "values")
  }
}

//project.afterEvaluate {
//  project.tasks.getByName("generateReleasePlayResources").dependsOn parseMetadata
//}

